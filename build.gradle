plugins {
    id 'java'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.11.0'
    id 'maven-publish'
}

pluginBundle {
    website = 'https://github.com/rcw3bb/simple-git'
    vcsUrl = 'https://github.com/rcw3bb/simple-git'
    tags = ['git', 'repository', 'source', 'source-control', 'versioning', 'simple-git', 'ronella']
}

gradlePlugin {
    plugins {
        simpleGit {
            id = 'xyz.ronella.simple-git'
            displayName = 'Simple Git Plugin'
            description = 'The plugin that allows you access to git commands inside gradle as task'
            implementationClass = 'xyz.ronella.gradle.plugin.simple.git.SimpleGitPlugin'
        }
    }
}

publishing {
    publications {

        repositories.maven {
            url "https://repo.ronella.xyz/archiva/repository/${version.toString().endsWith("SNAPSHOT") ? 'snapshots' : 'internal'}/"
            credentials {
                username "${archivaUsername}"
                password "${archivaPassword}"
            }
        }

        pluginMaven(MavenPublication) {
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'Simple Git Plugin'
                description = 'The plugin that allows you access to git commands inside gradle as task'
                url = 'https://github.com/rcw3bb/simple-git'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/rcw3bb/simple-git/blob/master/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'ron'
                        name = 'Ronaldo Webb'
                        email = 'ron@ronella.xyz'
                    }
                }
                scm {
                    url = 'https://github.com/rcw3bb/simple-git'
                }
            }

            artifacts {
                groupId 'xyz.ronella.gradle.plugin'
                artifactId project.name
                version version
                artifact "./build/libs/${project.name}-${version}.jar"
            }
        }
    }
}

sourceCompatibility = 1.8

repositories {
    maven {
        url 'https://repo.ronella.xyz/artifactory/java-central'
        credentials {
            username "${artifactoryUsername}"
            password "${artifactoryPassword}"
        }
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.5.2'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.5.2'

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.1.0'
}

test {
    useJUnitPlatform()
}